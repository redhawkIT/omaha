# Copyright 2008-2010 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================

Import('env')

local_env = env.Clone()

inputs = [
    'complete_wnd.cc',
    'owner_draw_title_bar.cc',
    'progress_wnd.cc',
    'splash_screen.cc',
    'ui.cc',
    'ui_displayed_event.cc',
    'ui_metrics.cc',
    'yes_no_dialog.cc',
    ]

# Need to look in output dir to find .h files generated by midl compiler.
# This also allows Hammer to understand dependencies between this subdir
# and the .idl files in the goopdate folder.
local_env['CPPPATH'] += ['$OBJ_ROOT']


# Build these into a static library.
local_env.ComponentStaticLibrary('ui', inputs)

ui_test_env = env.Clone()
ui_test_env.Append(
    CPPPATH = [
        # Need to look in output dir to find .h files generated by midl compiler.
        # This also allows Hammer to understand dependencies between this subdir
        # and the .idl files in the goopdate folder.
        '$OBJ_ROOT',
        ],
)

ui_test = ui_test_env.OmahaUnittest(
    name='omaha_ui_unittest',
    source=[
        'splash_screen_test.cc',
        'progress_wnd_unittest.cc',
        'yes_no_dialog_unittest.cc',
    ],
    LIBS=[
        '$LIB_DIR/client.lib',
        '$LIB_DIR/common.lib',       # Required by client.
        '$LIB_DIR/goopdate_lib.lib', # Required by splashscreen UT.
        '$LIB_DIR/logging.lib',      # Required by statsreport.
        '$LIB_DIR/omaha3_idl.lib',
        '$LIB_DIR/security.lib',
        '$LIB_DIR/statsreport.lib',  # Required by client.
        '$LIB_DIR/ui.lib',
        'crypt32.lib',
        'uxtheme.lib',
    ],
    all_in_one=False,
    COMPONENT_TEST_SIZE='small',
    is_small_tests_using_resources=True,
)

ui_test_env.Depends(ui_test, '$TESTS_DIR/goopdate.dll')

omaha_dll = '$STAGING_DIR/goopdate.dll'
if ui_test_env.IsCoverageBuild():
  ui_test_env.CopyFileToDirectory('$TESTS_DIR', omaha_dll)
else:
  ui_test_env.Replicate('$TESTS_DIR', omaha_dll)

